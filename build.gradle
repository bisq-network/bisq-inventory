plugins {
    id 'application'
    // alias(libs.plugins.openjfx)
    id 'bisq.post-build'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

group 'bisq'
version = file("bisq/desktop/src/main/resources/version.txt").text.trim()

application {
    project.mainClassName = 'bisq.inventory.InventoryMonitorMain'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

jar.manifest.attributes(
        "Implementation-Title": project.name,
        "Implementation-Version": version
)
/*
javafx {
    version = '11.0.2'
    modules = ['javafx.controls']
}
*/

configurations.all {
    exclude group: 'com.github.bisq-network:bitcoinj'
    exclude group: 'io.grpc'
    exclude group: 'org.fxmisc.easybind'
}

dependencies {
    implementation 'bisq:common'
    implementation 'bisq:core'
    implementation 'bisq:proto'
    implementation 'bisq:p2p'

    implementation libs.guava
    implementation(libs.guice) {
        exclude group: 'com.google.guava'
    }
    implementation libs.spark.core

    implementation libs.logback.core
    implementation libs.logback.classic
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    implementation libs.slf4j.api

    testAnnotationProcessor libs.lombok
    testCompileOnly libs.lombok
    testImplementation libs.junit.jupiter
}

clean.doFirst {
    // Remove the project root level 'bisq-*' scripts and 'lib' dir generated at the end of the assemble task.
    delete fileTree(dir: projectDir, include: ['bisq-inventory', 'bisq-inventory.bat']), 'lib'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
